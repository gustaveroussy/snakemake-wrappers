"""
This rule indexes the pbgzipped vcf file
"""
rule tabix_index:
    input:
        "{tool}/{subcommand}/{sample}.vcf.gz"
    output:
        "{tool}/{subcommand}/{sample}.vcf.gz.tbi"
    message:
        "Indexing {wildcards.sample} calls with Tabix "
        "(built with {wildcards.tool} {wildcards.subcommand})"
    threads: 1
    resources:
        mem_mb=lambda wildcards, attempt: min(attempt * 1024, 10240),
        time_min=lambda wildcards, attempt: attempt * 60,
        tmpdir="tmp"
    params:
        "-p vcf"
    log:
        "logs/{tool}/{subcommand}/tabix/index/{sample}.log"
    wrapper:
        "bio/tabix"


"""
This rule compress a provided VCF file with pbgzip
"""
rule pbgzip_compress:
    input:
        "{tool}/{subcommand}/{sample}.vcf"
    output:
        "{tool}/{subcommand}/{sample}.vcf.gz"
    message:
        "Compressing {wildcards.sample} VCF file, "
        "(built with {wildcards.tool} {wildcards.subcommand})"
    threads:
        config.get("threads", 20)
    resources:
        mem_mb=lambda wildcards, attempt: min(attempt * 1024, 10240),
        time_min=lambda wildcards, attempt: attempt * 30,
        tmpdir="tmp"
    params:
        ""
    log:
        "logs/{tool}/{subcommand}/pbgzip/{sample}.log"
    wrapper:
        "bio/compress/pbgzip"
