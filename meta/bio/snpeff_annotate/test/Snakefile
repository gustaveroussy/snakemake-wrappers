"""
This rule annotated using previously donwloaded reference annotations.
"""
rule snpeff:
    input:
        calls="calling/{sample}.vcf",
        db="snpeff/download/ebola_zaire"
    output:
        calls=temp("snpeff/calls/{sample}.vcf"),
        stats="snpeff/report/{sample}.html",
        csvstats=temp("snpeff/csvstats/{sample}.csv")
    message: "Annotating {wildcards.sample} with SnpEff"
    threads: 3
    resources:
        mem_mb=lambda wildcard, attempt: min(attempt * 4096, 15360),
        time_min=lambda wildcard, attempt: attempt * 90,
        tmpdir="tmp"
    params:
        extra=config.get("snpeff_extra", "")
    log:
        "logs/snpeff/annotate/{sample}.log"
    wrapper:
        "bio/snpeff/annotate"


"""
This rule download SnpEff database, which will be used for further annotations.

This rule is cached, since it should be done only once per reference genome.
"""
rule snpeff_download:
    output:
        directory("snpeff/download/{reference}")
    message: "Downloading {wildcards.reference} database for SnpEff"
    cache: True
    threads: 1
    resources:
        mem_mb=lambda wildcard, attempt: min(attempt * 1024, 2048),
        time_min=lambda wildcard, attempt: attempt * 120,
        tmpdir="tmp"
    params:
        reference="{reference}"
    log:
        "logs/snpeff/download/{reference}.log"
    wrapper:
        "bio/snpeff/download"
